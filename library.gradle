apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit"
}

compileKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        jvmTarget = "1.8"
        apiVersion = "1.4"
        languageVersion = "1.4"
    }
}

compileTestKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        jvmTarget = "1.8"
        apiVersion = "1.4"
        languageVersion = "1.4"
    }
}


java {
    withSourcesJar()
}

tasks.dokka {
    outputFormat = "html"
    outputDirectory = "$buildDir/javadoc"
}

tasks.register("dokkaJar", Jar) {
    group = JavaBasePlugin.DOCUMENTATION_GROUP
    description = "Assembles Kotlin docs with Dokka"
    classifier = "javadoc"
    from(tasks.dokka)
}

publishing {
    repositories {
        maven {
            name = "OSSRH"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }

    }

    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            artifact(dokkaJar)

            pom {
                name = 'Kohmon'
                description = 'Collection of kotlin common helper'
                url = 'https://github.com/mailtarget/kohmon'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'mtarget'
                        name = 'MTARGET Dev'
                        email = 'dev@mtarget.co'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com//mailtarget/kohmon.git'
                    url = 'https://github.com/mailtarget/kohmon'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}