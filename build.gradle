allprojects {
    group = "co.mtarget"
    version = "0.0.2-dev"

    repositories {
        jcenter()
        mavenCentral()
    }

}

subprojects {
    buildscript {
        ext {
            kotlin_version = '1.4.10'
            coroutine_version = '1.3.9'
            logback_version = '1.2.3'
            junit_version = '4.12'
        }

        repositories {
            jcenter()
            mavenCentral()
        }

        dependencies {
            classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
            classpath "org.jetbrains.dokka:dokka-gradle-plugin:$kotlin_version"
        }

    }

    apply plugin: 'maven-publish'
    apply plugin: 'java-library'
    apply plugin: 'signing'
    apply plugin: 'jacoco'

    java {
        withJavadocJar()
        withSourcesJar()
    }

    publishing {
        repositories {
            maven {
                name = "OSSRH"
                url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = System.getenv("MAVEN_USERNAME")
                    password = System.getenv("MAVEN_PASSWORD")
                }
            }

        }

        publications {
            gpr(MavenPublication) {
                from(components.java)

                pom {
                    name = 'Kohmon'
                    description = 'Collection of kotlin common helper'
                    url = 'https://github.com/mailtarget/kohmon'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'mtarget'
                            name = 'MTARGET Dev'
                            email = 'dev@mtarget.co'
                        }
                    }

                    scm {
                        connection = 'scm:git:git://github.com//mailtarget/kohmon.git'
                        url = 'https://github.com/mailtarget/kohmon'
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications.gpr
    }

}

